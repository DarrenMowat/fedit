
-- FOUL Math Library
-- Can be imported via 'import std/math'

import std/bool

-- Addition
add(x, Z)       = x
add(Z, y)       = y
add(S(x), S(y)) = S(S(add(x, y)))

-- Subtraction
-- Note: We don't support negative numbers. Z is returned if answer is negative
sub(x, Z)       = x
sub(Z, y)       = errorNegativeNumberError
sub(S(x), S(y)) = sub(x, y)

-- Multiplcation
mul(x, S(y))    = add(x, mul(x, y))
mul(_, _)       = Z 

-- Division a / b a, b
div(x, Z)       = errorDivisionByZero
div(Z, x)       = Z
div(n, d)       = cond (lt(n, d), Z, S(div(sub(n, d), d)))


-- Less Than
lt(Z, S(y))    = T
lt(x, Z)       = F
lt(S(x), S(y)) = lt(x, y)

-- Less Than Or Equal To
lte(x, y) = or(lt(x, y), eq(x, y))

-- More Than
mt(S(y), Z)    = T
mt(Z, x)       = F
mt(S(x), S(y)) = mt(x, y)

-- Less Than Or Equal To
mte(x, y) = or(mt(x, y), eq(x, y))

-- Number Equality 
eq(Z, Z) = T 
eq(S(x), S(y)) = eq(x, y)
eq(_, _)       = F

-- Square
square(x) = mul(x, x)

-- Infinty
infinity(a) = infinity(S(a))

-- Fibonacci Numbers
fib(0) = 0
fib(1) = 1
fib(S(S(x))) = add(fib(S(x)), fib(x)) 